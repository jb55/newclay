import containers.(push, reserve, Pusher);
import ctypes.(CString, CChar);
import meta.statics.(staticString?, staticStringSize);
import strings.encodings.(stringBytes, stringTypeEncoding, EncodedString?);
import strings.encodings.utf8.(UTF8);
import vectors.(Vector);
import values.(allValues?);
import unsafe.casts.(pointerCast);
import unsafe.coordinates.(begin, end);
import unsafe.valuesemantics.(destroy);


//
// String?
//

#String?('S) = Sequence?('S) and Char?(sequenceElementType('S));


//
// RandomAccessString alias
//

alias RandomAccessString = Vector[Char];


//
// String -- pushable and iterable UTF-8 encoded string with cached size
//

define String as RecordType(size:UInt, contents:UTF8[Vector[UInt8]]);

overload String(#'s) | staticString?('s)
    = String(*#staticStringSize('s), UTF8(Vector(stringBytes('s))));

overload String() = String(0u, UTF8(Vector[UInt8]()));

overload String(forward x:'S) --> returned:String
    | String?('S) and 'S != String
{
    returned <-- String();
    try {
        push(returned, x);
    } catch (ex) {
        destroy(returned);
        throw ex;
    }
}

overload String(x:'S)
    | EncodedString?('S, UTF8) and SizedSequence?('S) and 'S != String
    = String(size(x), UTF8(Vector(stringBytes(x))));

overload size(s:String) = s.size;
overload source(s:String) = source(s.contents);
overload sink(ref s:String) = Pusher(s);

overload #stringTypeEncoding(String) = UTF8;
overload stringBytes(s:String) = stringBytes(s.contents);

overload push(ref s:String, c:Char) {
    push(s.contents, c);
    s.size += 1u;
}
overload push(ref s:String, s2:'S)
    | String?('S)
{
    for (c in s2)
        push(s, c);
}
overload push(ref s:String, s2:'S)
    | String?('S) and SizedSequence?('S)
{
    push(s.contents, s2);
    s.size += size(s2);
}


//
// cat -- concatenate multiple String? types into a single String
//

cat(forward s1:'S1, forward ..ss:'SS) --> returned:String
    | String?('S1) and allValues?(String?, ..'SS)
{
    returned <-- String(s1);
    try {
        push(returned, ..ss);
    } catch (ex) {
        destroy(returned);
        throw ex;
    }
}


//
// some inappropriate touching to tack a null byte onto a String so it can be
// passed as a CString
//
// XXX requires the String to be a ref. can we make it also work for rvalue and const?
//

overload CString(ref s:String) {
    ref bytes = *s.contents;
    var beginByte = begin(bytes);
    var sz = size(bytes);
    reserve(bytes, sz + 1u);
    beginByte[sz] = 0_u8;
    return CString(pointerCast(Pointer[CChar], beginByte));
}

