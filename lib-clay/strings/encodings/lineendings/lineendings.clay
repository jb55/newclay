define WindowsLineEndingDecoder['I] as NewType('I);

overload WindowsLineEndingDecoder(forward s:'S) inline
    | Sequence?('S) and sequenceElementType('S) == Char
    = WindowsLineEndingDecoder[sequenceIteratorType('S)](iterator(s));

overload read1(ref s:WindowsLineEndingDecoder['I]) {
    ref iter = *s;
    if (hasFront?(iter)) {
        if (front(iter) == '\r') {
            incFront(iter);
            if (hasFront?(iter) and front(iter) == '\n') {
                incFront(iter);
                return just('\n');
            } else
                return just('\r');
        } else {
            var c = front(iter);
            incFront(iter);
            return just(c);
        }
    } else
        return nothing(Char);
}

define WindowsLineEndingEncoder['I] as NewType('I);

overload WindowsLineEndingEncoder(forward s:'S) inline
    | Sink?('S, Char)
    = WindowsLineEndingEncoder['S](s);

overload hasFront?(s:WindowsLineEndingEncoder['S]) = hasFront?(*s);
overload write1(ref s:WindowsLineEndingEncoder['S], c:Char) {
    ref sink = *s;

    if (c == '\n') {
        write1(sink, '\r');
        write1(sink, '\n');
    } else
        write1(sink, c);
}
