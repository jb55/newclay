import unsafe.memory.(allocateUninitializedValue, freeUninitializedValue);
import pointers.unique.*;
import unsafe.compositetypes.*;
import unsafe.valuesemantics.*;

define UniquePointer['T] as CompositeType(ptr:MPointer['T]);

alias MUniquePointer['T] = Maybe[UniquePointer['T]];

overload UniquePointer['T](f, forward ..args) inline
    | type(-> f(..args)) == 'T
{
    var ptr = allocateUninitializedValue('T);
    try {
        ptr^ <-- f(..args);
    } catch (ex) {
        freeUninitializedValue(ptr);
        throw ex;
    }
    return initializeRecord(UniquePointer['T], just(ptr));
}

overload UniquePointer(f, forward ..args) inline
    | defined?(-> f(..args))
    = UniquePointer[#type(-> f(..args))](f, ..args);

overload destroy(ref p:UniquePointer['T]) inline {
    maybe(p.ptr, (ptr) -> { destroy(ptr^); freeUninitializedValue(ptr); });
}

overload reset(ref p:UniquePointer['T]) inline {
    p.ptr <-- nothing(Pointer['T]);
}

overload dereference(p:UniquePointer['T]) inline = forward required(p.ptr)^;

overload equals?(p:UniquePointer['T], q:UniquePointer['T])
    = p.ptr == q.ptr;
