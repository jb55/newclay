import unsafe.casts.(pointerCast);
import values.(allValues?);

define hash;

#Hashable?('T) = callDefined?(equals?, 'T, 'T) and callDefined?(hash, 'T);

overload hash(x:'T) inline
    | Integer?('T) or Char?('T) or 'T == Bool
    = UInt(x);
overload hash(x:'P) inline | Pointer?('P) = pointerCast(UInt, x);
overload hash(x:Static['x]) = 0u;

hashValues(forward ..values) {
    var h = 0u;
    static for (forward v in ..values)
        h = 7u*h + 13u*hash(v);
    return h;
}
overload hashValues(forward value) inline = hash(value);

overload hash(x:Tuple[..'T]) inline
    | allValues?(Hashable?, ..'T)
    = hashValues(..*x);
overload hash(x:'R) inline
    | RecordType?('R) and allValues?(Hashable?, ..fieldTypes('R))
    = hashValues(..*x);
overload hash(x:'N) inline
    | NewType?('N) and Hashable?(underlyingType('N))
    = hash(*x);

hashSequence(seq:'S) | Sequence?('S) {
    var h = 0u;
    var f = 11u;
    for (forward ..v in seq) {
        h += f * hashValues(..v);
        f = 2u*f - 1u;
    }
    return h;
}

overload hash(x:Array['T,'n]) | Hashable?('T) = hashSequence(x);
