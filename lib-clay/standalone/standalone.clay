// callMain() is the true entry point for standalone executables
// this implementation calls the main() function with one of:
// - no arguments
// - a Sequence? type whose members are of a String? type, containing the commandline
//   arguments
// - an Int and Pointer[Pointer[CChar]], containing the raw original values of
//   argc and argv

import __main__.(main);
import ctypes.(CChar, CString);
import unsafe.casts.(pointerCast);
import unsafe.coordinates.(ContiguousCoordinate, CoordinateRange);

private exitValue() = 0;
overload exitValue(n) = Int(n);

define callMain;
overload callMain(cargc:Int, cargv:Pointer[Pointer[CChar]])
    | callDefined?(main)
{
    return exitValue(..main());
}
overload callMain(cargc:Int, cargv:Pointer[Pointer[CChar]])
    | callDefined?(main, CoordinateRange[ContiguousCoordinate[CString]])
{
    var beginArgv = pointerCast(ContiguousCoordinate[CString], cargv);
    return exitValue(..main(CoordinateRange(beginArgv, beginArgv + cargc)));
}
overload callMain(cargc:Int, cargv:Pointer[Pointer[CChar]])
    | callDefined?(main, Int, Pointer[Pointer[CChar]])
{
    return exitValue(..main(cargc, cargv));
}
