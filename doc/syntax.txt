

//
// top level
//

TopLevelItem -> GlobalSymbol
              | SymbolAttribute
              | StaticOverload
              | Overload
              | Import
              | Procedure
              | StaticFunction
              | ExternalProcedure
              | GlobalBinding
              | GlobalAlias
              | GlobalLiteralCodeBody

Visibility -> "public" | "private"

NamePath -> Identifier ("." Identifier)*

Import -> Visibility? "import" NamePath ImportSpec? ";"
ImportSpec -> "as" Identifier
            | "." "*"
            | "." "(" comma_list(SpecificImportSpec) ")"
SpecificImportSpec -> Visibility? Identifier ("as" Identifier)?

SymbolName -> Identifier SymbolParams?

GlobalSymbol -> Visibility? "define" SymbolName "as" Expr ";"
SymbolParams -> "[" variadic_list(PatternNameRef, PatternNameRef) "]"
SymbolAttribute -> "extend" TargetPattern Predicate? "with" Expr ";"

StaticOverloadDefinition -> "(" ListPattern? ")" Predicate? "=" ExprList ";"

StaticOverload -> "overload" "#" TargetPattern StaticOverloadDefinition

OverloadAttributes -> OverloadAttribute*
OverloadAttribute -> "inline" | "noreturn" | "nothrow"

OverloadDefinition -> "(" ArgumentList? ")" OverloadAttributes ReturnList?
                      Predicate? CodeBody

Overload -> "overload" TargetPattern OverloadDefinition

StaticFunction -> Visibility? "#" SymbolName StaticOverloadDefinition

Procedure -> Visibility? SymbolName OverloadDefinition

ExternalProcedure -> Visibility? "external" ExternalAttributes? Identifier
                     ExternalArgumentList OverloadAttributes ExternalReturn? CodeBody
ExternalAttributes -> "(" ExprList ")"
ExternalArgumentList -> "(" variadic_list(Argument, ()) ")"
ExternalReturn -> ":" Expr?

GlobalBinding -> Visibility? BindingKind SymbolName (":" Expr)? ("=" Expr)? ";"

GlobalAlias -> Visibility? "alias" SymbolName "=" Pattern ";"

GlobalLiteralCodeBody -> LiteralCodeBodyKind LiteralCodeBody ";"

Predicate -> "|" Expr

ArgumentList -> variadic_list2(Argument, MultiArgument)
Argument -> BoundArgument
          | StaticArgument;
BoundArgument -> ArgumentKind? Identifier (":" Pattern)?
StaticArgument -> "#" Pattern
MultiArgument -> ArgumentKind? ".." Identifier (":" PatternNameRef)?
ArgumentKind -> "const" | "ref" | "rvalue" | "forward"

ReturnList -> NamedReturnList | UnnamedReturnList

NamedReturnList -> "-->" (NamedReturnSpec | NamedVariadicReturnSpec)+
NamedReturnSpec -> ReturnKind? Identifier ":" Expr
NamedVariadicReturnSpec -> ReturnKind? ".." Identifier ":" Expr

UnnamedReturnList -> ":" (UnnamedReturnSpec | UnnamedVariadicReturnSpec)+
UnnamedReturnSpec -> ReturnKind? Expr
UnnamedVariadicReturnSpec -> ReturnKind? ".." Expr

ReturnKind -> "const" | "ref" | "rvalue"

CodeBody -> "=" ReturnExprList ";"
          | Block
          | LiteralCodeBodyKind LiteralCodeBody ";"
          | ";"

LiteralCodeBodyKind = "__c__" | "__asm__" | "__llvm__"

LiteralCodeBody -> """.*"""

ReturnExprList -> comma_list(ReturnExprListItem)
ReturnExprListItem -> ReturnExprKind? ExprListItem
ReturnExprKind -> "const" | "ref" | "rvalue" | "forward"



//
// patterns
//

ListPattern -> variadic_list(Pattern, PatternNameRef)
Pattern -> BoolLiteral
         | CharLiteral
         | IntLiteral
         | StringLiteral
         | IndexingPattern
         | TuplePattern
         | Identifier
         | PatternNameRef

IndexingPattern -> Identifier "[" ListPattern "]"

TuplePattern -> "[" ListPattern "]"

TargetPattern -> IndexingPattern
               | Identifier
               | PatternNameRef



//
// expressions
//

ExprList -> comma_list(ExprListItem)
ExprListItem -> ".." Expr
              | "(" ExprList ")"
              | StatementExprList
              | Expr

LiteralExpr -> BoolLiteral
             | CharLiteral
             | IntLiteral
             | FloatLiteral
             | StringLiteral

AtomicExpr -> LiteralExpr
            | SimpleNameRef
            | PatternNameRef
            | LambdaExpr
            | BracketedExpr
            | StatementExpr
            | TupleExpr

NameRef -> NamePath
SimpleNameRef -> Identifier
PatternNameRef -> "'" Identifier
BracketedExpr -> "(" Expr ")"
StatementExpr -> "(" Statement+ Expr ")"
StatementExprList -> "(" Statement+ ExprList ")"
TupleExpr -> "[" ExprList? "]"

PrefixExpr -> SignExpr
            | UnpackExpr
            | AddressOfExpr
            | SuffixExpr
            | StaticExpr

SignExpr -> ("+" | "-") PrefixExpr
UnpackExpr -> "*" PrefixExpr
AddressOfExpr -> "&" PrefixExpr
StaticExpr -> "#" PrefixExpr

SuffixExpr -> AtomicExpr Suffix*
Suffix -> IndexingSuffix
        | StaticIndexingSuffix
        | FieldRefSuffix
        | CallSuffix
        | DereferenceSuffix

IndexingSuffix -> "[" ExprList? "]"
StaticIndexingSuffix -> "." IntLiteral
FieldRefSuffix -> "." Identifier
CallSuffix -> "(" ExprList? ")"
DereferenceSuffix -> "^"

MulDivExpr -> PrefixExpr (("*" | "/" | "div" | "mod") PrefixExpr)*

AddSubExpr -> MulDivExpr (("+" | "-") MulDivExpr)*

CompareExpr -> AddSubExpr (("<" | "<=" | ">=" | ">") AddSubExpr)?

EqualityExpr -> CompareExpr (("==" | "!=") CompareExpr)?

NotExpr -> "not"? EqualityExpr

AndExpr -> NotExpr ("and" NotExpr)*

OrExpr -> AndExpr ("or" AndExpr)*

LambdaCodeBody -> ("=>" | "->") ReturnExprListItem
                | ("=>" | "->")? Block

LambdaArguments -> "(" ArgumentList? ")" 

LambdaExpr -> LambdaArguments? LambdaCodeBody

IfExpr -> "if" "(" Statement* Expr ")" Expr "else" Expr

PairExpr -> identifier ":" Expr

ThrowExpr -> "throw" Expr

Expr -> PairExpr
      | IfExpr
      | LambdaExpr
      | OrExpr
      | ThrowExpr


//
// statements
//

Block -> LabelDef? "{" Statement* "}"

Statement -> Block
           | BindingStatement
           | Assignment
           | GotoStatement
           | ReturnStatement
           | BreakStatement
           | ContinueStatement
           | IfStatement
           | ForStatement
           | StaticForStatement
           | WhileStatement
           | SwitchStatement
           | TryStatement
           | ExprStatement
           | EmptyStatement
           | UnreachableStatement

LabelDef -> "@" Identifier

BindingStatement -> BindingKind BindingList ";"

Assignment -> ExprList AssignmentOp ExprList ";"
AssignmentOp -> "<--" | "=" | "+=" | "-=" | "*=" | "/=" | "div" "=" | "mod" "="

GotoStatement -> "goto" Identifier ";"

ReturnStatement -> "return" ReturnExprList? ";"

BreakStatement -> "break" ("@" Identifier)? ";"

ContinueStatement -> "continue" ("@" Identifier)? ";"

IfStatement -> "if" "(" Statement* Expr ")" Statement
               ("else" Statement)?

ForStatement -> LabelDef? "for" "(" ArgumentList "in" Expr ")" Statement

StaticForStatement -> LabelDef? "static" "for" "(" Argument "in" ExprList ")" Statement

WhileStatement -> LabelDef? "while" "(" Statement* Expr ")" Statement

SwitchStatement -> "switch" "(" Statement* Expr ")"
                   ("case" "(" ExprList ")" Statement)+
                   ("else" Statement)?

TryStatement -> "try" Block ("catch" "(" BindingName ")" Block)+

ExprStatement -> Expr ";"

EmptyStatement -> ";"

UnreachableStatement -> "__unreachable__" ";"


//
// variable binding
//

BindingKind -> "var" | "const" | "ref" | "rvalue" | "forward"

BindingName -> Identifier (":" Expr)?

SingleBinding -> BindingName "=" Expr

BindingList -> comma_list(SingleBinding)
             | variadic_list(BindingName, Identifier) ("=" ExprList)?


//
// interactor input

InteractorInput -> InteractorImport
                 | ExprList

InteractorImport -> "import" NamePath ImportSpec? ";"


//
// misc
//

variadic_list(Head, Tail) -> variadic_list2(Head, ".." Tail)

variadic_list2(Head, Tail) -> Tail
                            | comma_list(Head) ("," Tail)?
