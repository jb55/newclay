
import newclay.common.*;
import newclay.core.*;
import newclay.loader.*;

dumpModuleValue(moduleValue:ModuleValue, depth:Int) {
    for (name, submodule in items(moduleValue.submodules)) {
        ref submoduleValue = nestedVariantAs(submodule.envEntry, ModuleValue);
        for (x in range(depth))
            print("  ");
        println(".", name, ": ", submoduleValue);
        dumpModuleValue(submoduleValue, depth+1);
    }
}

main(args) {
    if (size(args) != 2) {
        println("usage: ", args[0], " <file>");
        return -1;
    }
    try {
        var program = loadProgram(args[1]);
        for (moduleName, module in items(program.modules)) {
            println(moduleNameString(moduleName), ":");

            for (nameEnvRoot in [("public", module.publicEnvRoot), ("private", module.privateEnvRoot)]) {
                var name, root = ...unpack(nameEnvRoot);

                println("  ", name, " root:");
                for (name, genImport in items(root.generalImports))
                    println("    imports ", genImport.module.name, ".*");
                for (asName, specImport in items(root.specificImports)) {
                    println("    imports ", specImport.entry, " as ", asName);
                    if (nestedVariantIs?(specImport.entry, ModuleValue)) {
                        var moduleValue = nestedVariantAs(specImport.entry, ModuleValue);
                        dumpModuleValue(moduleValue, 3);
                    }
                }
            }


            for (nameEnv in [("public", module.publicEnv), ("private", module.privateEnv)]) {
                var name, env = ...unpack(nameEnv);
                println("  ", name, ":");
                for (entryName, entry in items(env))
                    println("    ", entryName, ": ", entry);
            }
        }
    }
    catch (e:ClayError) {
        displayError(e);
    }
    return 0;
}
