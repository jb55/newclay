import newclay.ast as ast;
import newclay.ir as ir;
import newclay.common.*;
import newclay.core.*;
import newclay.hooks.*;
import maybe.*;

alias RTValueMap = Map[RTValue, RTValue];
alias RTValueSet = Map[RTValue, Tuple[]];


//
// bind coalescing
//

record CoalesceContext (
    map:Pointer[RTValueMap],
    statementRvalues:RTValueSet,
);

overload CoalesceContext(map:RTValueMap) = CoalesceContext(&map, RTValueSet());
overload CoalesceContext(x:CoalesceContext);

private addStatementRvalues(cc:CoalesceContext, node) {
    for (input in ir.nodeInputs(node)) {
        remove(cc.statementRvalues, input);
    }

    for (output in ir.nodeOutputs(node)) {
        cc.statementRvalues[output] = ();
    }
}

coalesceBinding(cc:CoalesceContext, from:RTValue, to:RTValue) {
    assert(to.type == from.type, "coalescing invariant: binding types must match");

    var cFrom = coalesced(cc.map^, from);

    // rvalues can merge with previous rvalues.
    // permanent values can merge with previous rvalues (even if rvalue is a ref!)
    if (from.kind == RT_VALUE_RVALUE) {
        assert(localVariable?(to) or immediateValue?(to) or not inValues?(to.kind, RT_VALUE_CONST, RT_VALUE_REF),
            "coalescing invariant: rvalue cannot be bound to const or ref reference"
        );
        assert(cFrom.kind == RT_VALUE_RVALUE,
            "coalescing invariant: rvalue cannot be referenced after coalescing with non-rvalue (multiple reference to rvalue not allowed)"
        );

        if (to.kind == RT_VALUE_RVALUE)
            cc.map^[to] = cFrom;
        else if (variable?(cFrom)) {
            var newVar = RTValue(to.kind, to.type, cFrom.referent,
                maybe(to.name, x ref=> to.name, () ref=> cFrom.name)
            );

            cc.map^[cFrom] = newVar;
            cc.map^[to] = newVar;
        }
    // references can merge with previous references
    } else if (referenceVariable?(to)) {
        cc.map^[to] = cFrom;
    // stash non-coalesceable rvalues in the coalescing map so other branches don't
    // try to coalesce them
    } else if (to.kind == RT_VALUE_RVALUE)
        cc.map^[to] = to;
}

coalesceInitialize(cc:CoalesceContext, from:RTValue, to:RTValue) {
    assert(to.type == from.type, "coalescing invariant: initialize types must match");

    var cFrom = coalesced(cc.map^, from);

    // only coalesce initializations from rvalues introduced by the same statement
    if (null?(lookup(cc.statementRvalues, cFrom))) {
        return;
    }

    // if initialized from an rvalue, we can steal the rvalue's identity
    if (cFrom.kind == RT_VALUE_RVALUE and localVariable?(cFrom)) {
        cc.map^[cFrom] = to;
    }
}

// coalesce pass

coalesceBody(body:Vector[ir.Node]) {
    var coalescingMap = RTValueMap();
    var cc = CoalesceContext(coalescingMap);

    for (node in body)
        coalesceSubnode(cc, *node);

    return coalescingMap, coalescedNodes(coalescingMap, body);
}

private coalesceSubnode(cc:CoalesceContext, node) {
    coalesceNode(cc, node);
    addStatementRvalues(cc, node);
}

private coalesceNode(cc:CoalesceContext, node) { }
overload coalesceNode(cc:CoalesceContext, node:ir.Bind) {
    coalesceBinding(cc, node.input, node.output);
}
overload coalesceNode(cc:CoalesceContext, node:ir.Initialize) {
    coalesceInitialize(cc, node.input, node.output);
}
overload coalesceNode(cc:CoalesceContext, node:ir.If) {
    var thenmap = cc.map^;
    var elsemap = cc.map^;
    var thencc = CoalesceContext(thenmap);
    var elsecc = CoalesceContext(elsemap);

    for (subnode in node.thenBody)
        coalesceSubnode(thencc, *subnode);
    for (subnode in node.elseBody)
        coalesceSubnode(elsecc, *subnode);
    unifyBranchCoalescing(cc.map^, thencc.map^, elsecc.map^);
}
overload coalesceNode(cc:CoalesceContext, node:ir.While) {
    var subcc = CoalesceContext(cc.map^);
    for (subnode in node.conditionBody)
        coalesceSubnode(subcc, *subnode);
    for (subnode in node.body)
        coalesceSubnode(subcc, *subnode);
}
overload coalesceNode(cc:CoalesceContext, node:ir.Block) {
    var subcc = CoalesceContext(cc.map^);
    for (subnode in node.body)
        coalesceSubnode(subcc, *subnode);
}
overload coalesceNode(cc:CoalesceContext, node:ir.Statement) {
    var subcc = CoalesceContext(cc.map^);
    for (subnode in node.body)
        coalesceSubnode(subcc, *subnode);
}

private unifyBranchCoalescing(cmap:RTValueMap, thenCmap:RTValueMap, elseCmap:RTValueMap) {
    for (from, to in items(thenCmap))
        if (null?(lookup(cmap, from)) and null?(lookup(elseCmap, from)))
            cmap[from] = to;
    for (from, to in items(elseCmap))
        if (null?(lookup(cmap, from)) and null?(lookup(thenCmap, from)))
            cmap[from] = to;
}

// coalescing application pass

coalesced(cmap:RTValueMap, rtVar:RTValue) {
    var cVar = rtVar;
    var cVarp = lookup(cmap, cVar);
    while (not null?(cVarp)) {
        if (cVar == cVarp^)
            return cVar;
        cVar = cVarp^;
        cVarp = lookup(cmap, cVar);
    }
    return cVar;
}

private coalescedVars(cmap:RTValueMap, vs:RTValues)
    = map(v ref=> coalesced(cmap, v), vs);
overload coalescedVars(cmap:RTValueMap, vs:Vector[ir.OutputVariable])
    = map(o ref=> ir.OutputVariable(coalesced(cmap, o.value), o.returnByRef?), vs);
[T]
overload coalescedVars(cmap:RTValueMap, mvs:Maybe[T])
    = maybe(mvs, vs ref=> Maybe(coalescedVars(cmap, vs)));


private coalescedNodes(cmap:RTValueMap, nodes:Vector[ir.Node])
    = map(n ref=> makeCoalescedNode(cmap, *n), nodes);

private makeCoalescedNode(cmap, n) = ir.Node(coalescedNode(cmap, n));

private procedure coalescedNode;

overload coalescedNode(cmap:RTValueMap, node:ir.LiteralCodeBody) = node;

overload coalescedNode(cmap:RTValueMap, node:ir.Adopt) = ir.Adopt(coalesced(cmap, node.value));

overload coalescedNode(cmap:RTValueMap, node:ir.Consume) {
    assert(coalesced(cmap, node.value) == node.value, "coalescing invariant: Consumed values should never be referenced, so they should never be coalesced");
    return node;
}

overload coalescedNode(cmap:RTValueMap, node:ir.Call)
    = ir.Call(
        node.ast,
        coalescedVars(cmap, node.inputs),
        coalescedVars(cmap, node.outputs),
        node.specialization,
    );

overload coalescedNode(cmap:RTValueMap, node:ir.DereferenceCall)
    = ir.DereferenceCall(
        node.ast,
        coalesced(cmap, node.codePointer),
        coalescedVars(cmap, node.inputs),
        coalescedVars(cmap, node.outputs),
    );

overload coalescedNode(cmap:RTValueMap, node:ir.Block) ir.Block
    = ir.Block(
        node.label,
        coalescedNodes(cmap, node.body)
    );

overload coalescedNode(cmap:RTValueMap, node:ir.Statement) ir.Statement
    = ir.Statement(coalescedNodes(cmap, node.body));

overload coalescedNode(cmap:RTValueMap, node:ir.If) ir.If
    = ir.If(
        node.ast,
        coalesced(cmap, node.condition),
        coalescedNodes(cmap, node.thenBody),
        coalescedNodes(cmap, node.elseBody),
    );

overload coalescedNode(cmap:RTValueMap, node:ir.While) ir.While
    = ir.While(
        node.label,
        node.ast,
        coalescedNodes(cmap, node.conditionBody),
        coalesced(cmap, node.condition),
        coalescedNodes(cmap, node.body),
    );

overload coalescedNode(cmap:RTValueMap, node:ir.Bind) {
    var cInput = coalesced(cmap, node.input);
    var cOutput = coalesced(cmap, node.output);
    if (isNot?(cInput, cOutput))
        return ir.Node(ir.Bind(node.ast, cInput, cOutput));
    else if (node.input.kind == RT_VALUE_RVALUE)
        return ir.Node(ir.Adopt(cInput));
    else
        return ir.Node(ir.Nop());
}

overload coalescedNode(cmap:RTValueMap, node:ir.Not)
    = ir.Not(
        node.ast,
        coalesced(cmap, node.input),
        coalesced(cmap, node.output),
    );

overload coalescedNode(cmap:RTValueMap, node:ir.AddressOf)
    = ir.AddressOf(
        node.ast,
        coalesced(cmap, node.input),
        coalesced(cmap, node.output),
    );

overload coalescedNode(cmap:RTValueMap, node:ir.AddressOfCode)
    = ir.AddressOfCode(
        node.ast,
        node.specialization,
        coalesced(cmap, node.output),
    );

overload coalescedNode(cmap:RTValueMap, node:ir.AddressOfExternalCode)
    = ir.AddressOfExternalCode(
        node.ast,
        node.specialization,
        coalesced(cmap, node.output),
    );

overload coalescedNode(cmap:RTValueMap, node:ir.Initialize) {
    var cInput = coalesced(cmap, node.input);
    var cOutput = coalesced(cmap, node.output);

    if (isNot?(node.input, node.output) and is?(cInput, cOutput))
        return ir.Node(ir.Nop());
    else
        return ir.Node(ir.Initialize(node.ast, cInput, cOutput));
}

overload coalescedNode(cmap:RTValueMap, node:ir.Break) = node;
overload coalescedNode(cmap:RTValueMap, node:ir.Continue) = node;
overload coalescedNode(cmap:RTValueMap, node:ir.Nop) = node;

overload coalescedNode(cmap:RTValueMap, node:ir.Throw)
    = ir.Throw(node.ast, coalesced(cmap, node.exception));

coalesceSpecialization(s:Specialization) {
    var coalescingMap, coalescedBody = ...coalesceBody(just(s.body));

    s.body = Maybe(move(coalescedBody));
    s.internalArgs = coalescedVars(coalescingMap, s.internalArgs);

    assert(s.internalNamedResults == coalescedVars(coalescingMap, s.internalNamedResults),
        "coalescing invariant: named return values should not be coalesced",
    );
    assert(s.results == coalescedVars(coalescingMap, s.results),
        "coalescing invariant: return values should not be coalesced",
    );
}


//
// optimizeSpecialization
//

optimizeSpecialization(s:Specialization) {
    coalesceSpecialization(s);
}
