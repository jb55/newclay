
import maybe.*;
import newclay.lexer.*;
import remote.marshaling.*;
import newclay.diagnostics.(computeLocation);



//
// ModuleName
//

record ModuleName(path: Vector[String]);


//
// TokenRange
//

alias TokenRange = Range[SizeT];



//
// AST nodes
//

record Location (
    tokens: SourceTokens,
    tokenRange: TokenRange,
    mainToken: SizeT,
);

private ast(...fields) = referenceTypeWithPredicate(
    ASTNode?,
    location: Location,
    ...fields,
);

procedure ASTNode?;
overload ASTNode?(x) = false;

[T, ...A | ASTNode?(T)
           and (Tuple[...ReferenceTypeFieldTypes(T)] ==
                Tuple[Location, ...A])]
callbyname overload T(...args:A) {
    alias tokenRange = TokenRange(SizeT(0), SizeT(0));
    return T(Location(SourceTokens(), tokenRange, SizeT(0)), ...args);
}

[T | ASTNode?(T)]
astFields(x:T) = forward ...dropValues(static 1, ...referenceTypeFields(x));

[T | ASTNode?(T)]
overload printReprTo(stream, x:T) {
    printTo(stream, T);
    printReprValuesTo(stream, ...astFields(x));
}

[T | ASTNode?(T)]
overload marshaledSize(context:MarshalContext, x:T) MarshalSize
    = foldValues(add,
        ...mapValues(f ref=> marshaledSize(context, f), ...astFields(x)),
    );

[T | ASTNode?(T)]
overload marshalTo(context:MarshalContext, buffer_:Pointer[Byte], x:T) buffer:Pointer[Byte] {
    buffer <-- buffer_;
    marshalMemberwise(context, buffer,
        ...astFields(x),
    );
}

[T | ASTNode?(T)]
overload unmarshalFrom(context:UnmarshalContext, begin_:Pointer[Byte], end:Pointer[Byte], static T)
    returned:T, begin:Pointer[Byte]
{
    begin <-- begin_;
    // XXX should get tokens pointer from SourceAST context if available
    returned <-- allocateUninitializedRefUnsafe(T);
    returned.location <-- Location();
    unmarshalMemberwise(context, begin, end,
        ...astFields(returned),
    );
}



//
// getLocation, setLocation, assignLocation
//

procedure getLocation;
overload getLocation(x) = x.location;
overload getLocation(x:Expr) = getLocation(*x);
overload getLocation(x:Statement) = getLocation(*x);
overload getLocation(x:ExprListItem) = getLocation(*x);
overload getLocation(x:Pattern) = getLocation(*x);
overload getLocation(x:CodeBody) = getLocation(*x);
overload getLocation(x:Argument) = getLocation(*x);
overload getLocation(x:Location) = x;

procedure setLocation;
overload setLocation(x, forward location) {
    x.location = location;
}
overload setLocation(x:Expr, forward location) {
    setLocation(*x, location);
}
overload setLocation(x:ExprListItem, forward location) {
    setLocation(*x, location);
}
overload setLocation(x:Pattern, forward location) {
    setLocation(*x, location);
}

assignLocation(destNode, srcNode) {
    setLocation(destNode, getLocation(srcNode));
}



//
// getFileLocation
//

getFileLocation(x) = ...getFileLocation2(getLocation(x));

private getFileLocation2(location) {
    if (null?(location.tokens))
        return String("(unknown)"), 1, 0;

    ref path = location.tokens.file.path;
    var data = location.tokens.file.data;
    var tokenOffset = location.tokenRange.begin;
    var offset = location.tokens.vector[tokenOffset].indexRange.begin;
    var line, column, tabs = ...computeLocation(data, offset);

    return path, line+1, column + 7*tabs;
}


//
// getSourceString
//

getSourceString(x) {
    return getSourceStringRange(x, x);
}

overload getSourceString(x:ExprList) {
    if (empty?(x))
        return String("");
    return getSourceStringRange(front(x), back(x));
}

getSourceStringRange(firstNode, lastNode) {
    var firstLocation = getLocation(firstNode);
    var lastLocation = getLocation(lastNode);
    assert(not null?(firstLocation.tokens), Type(firstNode), " token has no bounds");
    assert(not null?(lastLocation.tokens), Type(lastNode), " token has no bounds");
    assert(firstLocation.tokens == lastLocation.tokens, "token spans different source files?!");
    var sourceTokens = firstLocation.tokens;
    var firstTokenIndex = firstLocation.tokenRange.begin;
    var lastTokenIndex = lastLocation.tokenRange.end;

    var beginChar, endChar = sourceTokens.vector[firstTokenIndex].range.begin,
                             sourceTokens.vector[lastTokenIndex].range.end - 1;
    return slice(sourceTokens.file.data, beginChar, endChar);
}


//
// VariadicList
//

record VariadicList[T] (
    items: Vector[T],
    variadicItem: Maybe[T],
);

[T] emptyVariadicList?(vl:VariadicList[T])
    = empty?(vl.items) and nothing?(vl.variadicItem);



//
// Identifier
//

record Identifier = ast(
    str: String,
);

[S | String?(S) and (S != String)]
overload Identifier(x:S) = Identifier(String(x));



//
// ExprList
//

alias ExprList = Vector[ExprListItem];

record SubExprList = ast(
    exprList: ExprList,
);

variant ExprListItem = UnpackExpr
                     | SubExprList
                     | Expr
                     | StatementExprList
                     ;

record UnpackExpr = ast(
    expr: Expr,
);



//
// Expr
//

variant Expr = BoolLiteral
             | CharLiteral
             | IntLiteral
             | FloatLiteral
             | StringLiteral
             | NameRef
             | SimpleNameRef
             | PatternNameRef
             | StatementExpr
             | TupleExpr
             | Call
             | Indexing
             | StaticIndexing
             | FieldRef
             | AndExpr
             | OrExpr
             | NotExpr
             | LambdaExpr
             | IfExpr
             | ThrowExpr
             | UnaryOpExpr
             | BinaryOpExpr
             | StaticExpr
             | PairExpr
             ;

overload Expr() = Expr(BoolLiteral());

record BoolLiteral = ast(
    value: Bool,
);

record CharLiteral = ast(
    value: String,
);

record IntLiteral = ast(
    negative?: Bool,
    value: String,
);

record FloatLiteral = ast(
    negative?: Bool,
    value: String,
);

record StringLiteral = ast(
    value: String,
);

record UnresolvedEnvRef ();
record LocalEnvRef ();
record ModuleEnvRef (module: ModuleName, name: String);
record ModulePathRef (path: Vector[Identifier]);
record ModuleAliasRef (module: ModuleName);

variant EnvRef = UnresolvedEnvRef
               | LocalEnvRef
               | ModuleEnvRef
               | ModulePathRef
               | ModuleAliasRef;
overload EnvRef() = EnvRef(UnresolvedEnvRef());

record NameRef = ast(
    env: EnvRef,
    name: Vector[Identifier],
);
overload NameRef(name:Vector[Identifier]) = NameRef(EnvRef(), name);
overload NameRef(name:Identifier) = NameRef(EnvRef(), Vector[Identifier](name));
[S | String?(S)]
overload NameRef(name:S) = NameRef(Identifier(String(name)));

record SimpleNameRef = ast(
    env: EnvRef,
    name: Identifier,
);

overload SimpleNameRef(name:Identifier) = SimpleNameRef(EnvRef(), name);
[S | String?(S)]
overload SimpleNameRef(name:S) = SimpleNameRef(EnvRef(), Identifier(String(name)));

record PatternNameRef = ast(
    name: Identifier,
);

record StatementExprList = ast(
    statements: Vector[Statement],
    exprList: ExprList,
);

record StatementExpr = ast(
    statements: Vector[Statement],
    expr: Expr,
);

record TupleExpr = ast(
    args: ExprList,
);

record Call = ast(
    expr: Expr,
    args: ExprList,
);

record Indexing = ast(
    expr: Expr,
    args: ExprList,
);

record StaticIndexing = ast(
    expr: Expr,
    index: IntLiteral,
);

record FieldRef = ast(
    expr: Expr,
    field: Identifier,
);

record AndExpr = ast(
    expr1: Expr,
    expr2: Expr,
);

record OrExpr = ast(
    expr1: Expr,
    expr2: Expr,
);

record NotExpr = ast(
    expr: Expr,
);

enum OperatorKind {
    OP_PLUS,
    OP_MINUS,
    OP_UNPACK,
    OP_ADDRESS_OF,
    OP_DEREFERENCE,
    OP_MULTIPLY,
    OP_DIVIDE,
    OP_QUOTIENT,
    OP_REMAINDER,
    OP_ADD,
    OP_SUBTRACT,
    OP_LESSER?,
    OP_LESSER_EQUALS?,
    OP_GREATER?,
    OP_GREATER_EQUALS?,
    OP_EQUALS?,
    OP_NOT_EQUALS?,
}

record StaticExpr = ast(
    expr: Expr,
);

record UnaryOpExpr = ast(
    operator: OperatorKind,
    expr: Expr,
);

record BinaryOpExpr = ast(
    operator: OperatorKind,
    expr1: Expr,
    expr2: Expr,
);

enum LambdaCaptureKind {
    LAMBDA_CAPTURE_BY_VALUE,
    LAMBDA_CAPTURE_BY_REFERENCE,
}

record StatementReturnExprList = ast(
    statements: Vector[Statement],
    exprList: ReturnExprList,
);

record LambdaExpr = ast(
    captureKind: LambdaCaptureKind,
    args: ArgumentList,
    body: CodeBody,
    capturedNames: HashMap[String, Tuple[]],
    capturedOrder: Vector[String],
);
overload LambdaExpr(captureKind, args, body)
    = LambdaExpr(
        captureKind,
        args,
        body,
        HashMap[String,Tuple[]](),
        Vector[String](),
    );

record IfExpr = ast(
    condition: StatementExpr,
    thenPart: Expr,
    elsePart: Expr,
);

record ThrowExpr = ast(
    value: Expr,
);

record PairExpr = ast(
    name: Identifier,
    expr: Expr,
);


//
// ListPattern
//

alias ListPattern = VariadicList[Pattern];

variant Pattern = BoolLiteral
                | CharLiteral
                | IntLiteral
                | StringLiteral
                | NameRef
                | PatternNameRef
                | IndexingPattern
                | TuplePattern
                ;

overload Pattern() = Pattern(BoolLiteral());

record IndexingPattern = ast(
    base: Expr,
    args: ListPattern,
);

record TuplePattern = ast(
    elements: ListPattern,
);



//
// TopLevelItem
//

variant TopLevelItem = Symbol
                     | Attribute
                     | StaticOverload
                     | Overload
                     | Import
                     | Procedure
                     | StaticFunction
                     | ExternalProcedure
                     | GlobalLiteralCodeBody
                     | GlobalBinding
                     | GlobalAlias
                     ;

enum VisibilityKind {
    VIS_PUBLIC,
    VIS_PRIVATE,
}

record SymbolName = ast(
    name: Identifier,
    params: VariadicList[Identifier],
);

record Symbol = ast(
    visibility: VisibilityKind,
    name: SymbolName,
    expr: Maybe[ExprList],
);

record Attribute = ast(
    target: Pattern,
    predicate: Maybe[Expr],
    body: Expr,
);

record StaticOverloadDefinition = ast(
    args: ListPattern,
    predicate: Maybe[Expr],
    body: ExprList,
);

record StaticOverload = ast(
    target: Pattern,
    definition: StaticOverloadDefinition,
);

enum OverloadAttributeKind { OVERLOAD_INLINE, OVERLOAD_NORETURN, OVERLOAD_NOTHROW }

record OverloadAttribute = ast(
    kind: OverloadAttributeKind,
);

variant ReturnList = Nothing
                   | Vector[NamedReturn]
                   | Vector[ReturnSpec];

record OverloadDefinition = ast(
    args: ArgumentList,
    attributes: Vector[OverloadAttribute],
    returns: ReturnList,
    predicate: Maybe[Expr],
    body: CodeBody,
);

record Overload = ast(
    target: Pattern,
    definition: OverloadDefinition,
);

record StaticFunction = ast(
    visibility: VisibilityKind,
    name: SymbolName,
    definition: StaticOverloadDefinition,
);

record Procedure = ast(
    visibility: VisibilityKind,
    name: SymbolName,
    definition: OverloadDefinition,
);

record ExternalReturn = ast(
    returnType: Maybe[Expr],
);

record ExternalProcedure = ast(
    visibility: VisibilityKind,
    externalAttributes: Maybe[ExprList],
    attributes:Vector[OverloadAttribute],
    name: Identifier,
    arguments: ExternalArgumentList,
    returns: Maybe[ExternalReturn],
    body: CodeBody,
);

record ExternalArgument = ast(
    name: Identifier,
    type: Expr,
);

record ExternalArgumentList = ast(
    items: Vector[ExternalArgument],
    variadic?: Bool,
);

record SingleGlobalBinding = ast(
    name: SymbolName,
    type: Maybe[Expr],
    value: Maybe[Expr],
);

record GlobalBinding = ast(
    visibility: VisibilityKind,
    kind: BindingKind,
    bindings: Vector[SingleGlobalBinding],
);

record GlobalAlias = ast(
    visibility: VisibilityKind,
    name: SymbolName,
    value: Pattern,
);

record GlobalLiteralCodeBody = ast(
    codeBody: LiteralCodeBody,
);

record Import = ast(
    visibility: VisibilityKind,
    name: Vector[Identifier],
    spec: ImportSpec,
);

variant ImportSpec = GeneralImportSpec
                   | SpecificImportSpecs
                   | QualifiedImportSpec
                   | AliasedImportSpec
                   ;
alias SpecificImportSpecs = Vector[SpecificImportSpec];

record GeneralImportSpec = ast();
record SpecificImportSpec = ast(
    visibility: VisibilityKind,
    name: Identifier,
    asName: Maybe[Identifier],
);
record QualifiedImportSpec = ast();
record AliasedImportSpec = ast(
    asName: Identifier,
);

enum ArgumentKind {
    ARG_CONST,
    ARG_REF,
    ARG_RVALUE,
    ARG_FORWARD,
}

variant Argument = BoundArgument
                 | StaticArgument;

record BoundArgument = ast(
    kind: Maybe[ArgumentKind],
    name: Identifier,
    type: Maybe[Pattern],
);

record StaticArgument = ast(
    pattern: Pattern,
);

alias ArgumentList = VariadicList[Argument];

enum ReturnKind {
    RETURN_CONST,
    RETURN_REF,
    RETURN_RVALUE,
}

enum ReturnArityKind {
    RETURN_SINGLE,
    RETURN_MULTI,
}

record ReturnSpec = ast(
    kind: Maybe[ReturnKind],
    arityKind: ReturnArityKind,
    type: Expr,
);

record NamedReturn = ast(
    name: Identifier,
    spec: ReturnSpec,
);

variant CodeBody = UndefinedCodeBody
                 | ExprCodeBody
                 | Block
                 | LiteralCodeBody
                 ;

record UndefinedCodeBody = ast();

record ExprCodeBody = ast(
    exprs: ReturnExprList,
);

alias ReturnExprList = Vector[ReturnExprListItem];

record ReturnExprListItem = ast(
    kind: Maybe[ReturnExprKind],
    item: ExprListItem,
);

enum ReturnExprKind {
    RETURN_EXPR_CONST,
    RETURN_EXPR_REF,
    RETURN_EXPR_RVALUE,
    RETURN_EXPR_FORWARD,
}

enum LiteralCodeBodyKind {
    LITERAL_C,
    LITERAL_LLVM,
    LITERAL_ASM,
}

record LiteralCodeBody = ast(
    kind: LiteralCodeBodyKind,
    code: String,
);


//
// statements
//

record Block = ast(
    label: Maybe[LabelDef],
    statements: Vector[Statement],
);

variant Statement = Block
                  | BindingStatement
                  | AssignmentStatement
                  | ReturnStatement
                  | BreakStatement
                  | ContinueStatement
                  | IfStatement
                  | ForStatement
                  | StaticForStatement
                  | WhileStatement
                  | SwitchStatement
                  | TryStatement
                  | ExprStatement
                  | EmptyStatement
                  | UnreachableStatement
                  ;

record LabelDef = ast(
    name: Identifier
);

enum BindingKind {
    BINDING_VAR,
    BINDING_CONST,
    BINDING_REF,
    BINDING_RVALUE,
    BINDING_FORWARD,
}

record BindingStatement = ast(
    kind: BindingKind,
    bindings: BindingList,
);

variant BindingList = SingleBindingList
                    | MultipleBindingList
                    ;

record BindingName = ast(
    name: Identifier,
    type: Maybe[Expr],
);

record SingleBinding = ast(
    name: BindingName,
    value: Expr,
);

alias SingleBindingList = Vector[SingleBinding];

record MultipleBindingList = ast(
    names: VariadicList[BindingName],
    values: Maybe[ExprList],
);

enum AssignmentKind {
    ASSIGNMENT_ASSIGN,
    ASSIGNMENT_ADD,
    ASSIGNMENT_SUBTRACT,
    ASSIGNMENT_MULTIPLY,
    ASSIGNMENT_DIVIDE,
    ASSIGNMENT_QUOTIENT,
    ASSIGNMENT_REMAINDER,
    ASSIGNMENT_INITIALIZE,
}

record AssignmentStatement = ast(
    kind: AssignmentKind,
    to: ExprList,
    from: ExprList,
);

record ReturnStatement = ast(
    values: ReturnExprList,
);

record BreakStatement = ast(
    label: Maybe[Identifier],
);
record ContinueStatement = ast(
    label: Maybe[Identifier],
);

record IfStatement = ast(
    condition: StatementExpr,
    thenBody: Statement,
    elseBody: Maybe[Statement],
);

record ForStatement = ast(
    label: Maybe[LabelDef],
    bodyBindings: ArgumentList,
    sequenceValue: Expr,
    body: Statement,
);

record StaticForStatement = ast(
    label: Maybe[LabelDef],
    bodyBinding: Argument,
    values: ExprList,
    body: Statement,
);

record WhileStatement = ast(
    label: Maybe[LabelDef],
    condition: StatementExpr,
    body: Statement,
);

record SwitchStatement = ast(
    value: StatementExpr,
    caseBlocks: Vector[CaseBlock],
    defaultBlock: Maybe[Statement],
);

record CaseBlock = ast(
    values: ExprList,
    body: Statement,
);

record TryStatement = ast(
    body: Block,
    catchBlocks: Vector[CatchBlock],
);

record CatchBlock = ast(
    binding: BindingName,
    body: Block,
);

record ExprStatement = ast(
    expr: Expr,
);

record EmptyStatement = ast();

record UnreachableStatement = ast();


//
// interactor input
//

variant InteractorInput = InteractorImport
                        | ExprList;

record InteractorImport = ast(
    name: Vector[Identifier],
    spec: ImportSpec,
);


//
// SourceAST
//

record SourceAST = referenceType(
    tokens: SourceTokens,
    topLevelItems: Vector[TopLevelItem],
);
